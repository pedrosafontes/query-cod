?query: set_op

?set_op: join
       | set_op _UNION join     -> union
       | set_op _DIFF join      -> difference
       | set_op _INTERSECT join -> intersection
       | set_op _CARTESIAN join -> cartesian

?join: unary_op
     | join _NATURAL_JOIN unary_op  -> natural_join
     | join _theta_join_op unary_op -> theta_join
     | join _SEMIJOIN unary_op      -> semi_join
     | join _DIV unary_op           -> division

?unary_op: projection
         | selection
         | grouped_aggregation
         | topn
         | subquery

?subquery: relation
          | _LPAREN query _RPAREN

projection: _PI _subscript{nonempty_list{attribute}} unary_op

selection: _SIGMA _subscript{bool_expr} unary_op

?bool_expr: and_expr
          | bool_expr "\\lor" and_expr -> or_

?and_expr: bool_atom
         | and_expr "\\land" bool_atom -> and_

?bool_atom: "\\lnot" bool_atom -> not_
          | _LPAREN bool_expr _RPAREN
          | comparison
          | attribute

comparison: value comparator value
comparator: "=" -> eq
          | "\\neq" -> neq
          | ("<" | "\\lt") -> lt
          | "\\leq" -> leq
          | (">" | "\\gt") -> gt
          | "\\geq" -> geq

?value: SIGNED_FLOAT  -> float
      | SIGNED_INT -> int
      | _text{ESCAPED_STRING} -> string
      | attribute

grouped_aggregation: _GAMMA _subscript{_grouped_aggregation_params} unary_op
_grouped_aggregation_params: _LPAREN _grouping_attrs "," _aggregations _RPAREN
_grouping_attrs: _LPAREN list{attribute} _RPAREN
_aggregations: _LPAREN nonempty_list{aggregation} _RPAREN
aggregation: _LPAREN attribute "," aggregation_function "," attribute _RPAREN
aggregation_function: "count" -> count
                    | "sum" -> sum
                    | "avg" -> avg
                    | "min" -> min
                    | "max" -> max

topn: _TOPN _subscript{_topn_params} unary_op
_topn_params: _LPAREN INT "," attribute _RPAREN

_theta_join_op: _overset{bool_expr, "\\bowtie"}

relation: _identifier
attribute: (relation ".")? _identifier
_identifier: CNAME | _text{IDENT}

placeholder: "\\placeholder" "{" CNAME? "}"

_PI: "\\pi"
_SIGMA: "\\sigma"
_GAMMA: "\\Gamma"
_TOPN: "\\operatorname{T}"
_UNION: "\\cup"
_DIFF: "-"
_INTERSECT: "\\cap"
_CARTESIAN: "\\times"
_NATURAL_JOIN: "\\Join"
_SEMIJOIN: "\\ltimes"
_DIV: "\\div"

nonempty_list{item}: _nonempty_list_helper{item} -> item_list
list{item}: -> item_list
          | _nonempty_list_helper{item} -> item_list
_nonempty_list_helper{item}: item ("," item)*

_subscript{content}: "_" "{" content "}"
_overset{superscript, operator}: "\\overset" "{" superscript "}" "{" operator "}"
_text{content}: "\\text" "{" content "}"

IDENT: /[a-zA-Z_][a-zA-Z0-9_\\]*/
_LPAREN: ("\\left")? "("
_RPAREN: ("\\right")? ")"

ESCAPED_STRING: "'" _STRING_ESC_INNER "'"

%import common.CNAME
%import common.INT
%import common.SIGNED_INT
%import common.SIGNED_FLOAT
%import common._STRING_ESC_INNER
%import common.WS
%ignore WS
